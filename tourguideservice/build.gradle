plugins {
    id 'org.springframework.boot' version '2.6.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id "com.avast.gradle.docker-compose" version "0.15.2"
}

group = 'com.tourguide'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

ext {
    set('springCloudVersion', "2021.0.1")
}

dependencies {
    implementation project(':shared')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'io.github.openfeign:feign-okhttp'

    implementation 'org.javamoney:moneta:1.4.2'
    implementation 'com.jsoniter:jsoniter:0.9.23'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform() {
        excludeTags 'integration'
        excludeTags 'performance'
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform() {
        includeTags 'integration'
    }
}

task performanceTest(type: Test) {
    useJUnitPlatform() {
        includeTags 'performance'
    }
}

dockerCompose {
    integration {
        // Define the docker compose file for integration testing
        useComposeFiles = ["${buildDir}/resources/main/integrationTest/docker-compose.yml"]
        isRequiredBy(integrationTest)
        isRequiredBy(performanceTest)
    }
}


jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "tourguideservice/utils/InternalTestHelper*"
            ])
        }))
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
        }
    }
}
